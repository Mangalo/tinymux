%{
#include "omega.h"
#include "t5xgame.h"
int t5xlex();
void t5xerror(const char *s);
%}

%name-prefix="t5x"
%union {
    char                   *p;
    int                     i;
    T5X_ATTRINFO           *pai;
    vector<T5X_ATTRINFO *> *pvai;
    T5X_LOCKEXP            *ple;
}

%token EOD 
%token <p> STRING
%token <i> INTEGER DBREF OBJECT XHDR SIZEHINT NEXTATTR RECORDPLAYERS
%token <i> ATTRNAME
%type <pai> attr
%type <pvai> attrlist
%type <ple> lockexp
%start start

%%
start
:
    XHDR        { g_t5xgame.SetFlags($1); }
    sections
    EOD
;

sections
:
    section
|
    sections section
;

section
:
|
    SIZEHINT       { g_t5xgame.SetSizeHint($1); }
|
    NEXTATTR       { g_t5xgame.SetNextAttr($1); }
|
    RECORDPLAYERS  { g_t5xgame.SetRecordPlayers($1); }
|
    ATTRNAME STRING { g_t5xgame.AddNumAndName($1, $2); }
|
    objectlist
;

objectlist
:
    object
|
    objectlist object
;

object
:
    OBJECT   STRING INTEGER INTEGER
    INTEGER INTEGER INTEGER INTEGER
    lockexp
    INTEGER INTEGER INTEGER INTEGER
    INTEGER INTEGER INTEGER INTEGER
    attrlist '<' {
                     T5X_OBJECTINFO *oi = new T5X_OBJECTINFO;
                     oi->SetRef($1);
                     oi->SetName($2);
                     oi->SetLocation($3);
                     oi->SetZone($4);
                     oi->SetContents($5);
                     oi->SetExits($6);
                     oi->SetLink($7);
                     oi->SetNext($8);
                     oi->SetUseLock($9);
                     oi->SetOwner($10);
                     oi->SetParent($11);
                     oi->SetPennies($12);
                     oi->SetFlags1($13);
                     oi->SetFlags2($14);
                     oi->SetFlags3($15);
                     oi->SetPowers1($16);
                     oi->SetPowers2($17);
                     if (NULL != $18)
                     {
                         oi->SetAttrs($18->size(), $18);
                     }
                     g_t5xgame.AddObject(oi);
                 } 
|
    OBJECT   STRING INTEGER INTEGER
    INTEGER INTEGER INTEGER INTEGER
    INTEGER INTEGER INTEGER INTEGER
    INTEGER INTEGER INTEGER INTEGER
    attrlist '<' {
                     T5X_OBJECTINFO *oi = new T5X_OBJECTINFO;
                     oi->SetRef($1);
                     oi->SetName($2);
                     oi->SetLocation($3);
                     oi->SetZone($4);
                     oi->SetContents($5);
                     oi->SetExits($6);
                     oi->SetLink($7);
                     oi->SetNext($8);
                     oi->SetOwner($9);
                     oi->SetParent($10);
                     oi->SetPennies($11);
                     oi->SetFlags1($12);
                     oi->SetFlags2($13);
                     oi->SetFlags3($14);
                     oi->SetPowers1($15);
                     oi->SetPowers2($16);
                     if (NULL != $17)
                     {
                         oi->SetAttrs($17->size(), $17);
                     }
                     g_t5xgame.AddObject(oi);
                 } 
;

lockexp:
    '(' '=' lockexp ')'          { $$ = new T5X_LOCKEXP; $$->SetIs($3);      }
|
    '(' '+' lockexp ')'          { $$ = new T5X_LOCKEXP; $$->SetCarry($3);    }
|
    '(' '@' lockexp ')'          { $$ = new T5X_LOCKEXP; $$->SetIndir($3);    }
|
    '(' '$' lockexp ')'          { $$ = new T5X_LOCKEXP; $$->SetOwner($3);    }
|
    '(' lockexp '&' lockexp ')'  { $$ = new T5X_LOCKEXP; $$->SetAnd($2,$4);   }
|
    '(' lockexp '|' lockexp ')'  { $$ = new T5X_LOCKEXP; $$->SetOr($2,$4);    }
|
    '(' '!' lockexp ')'          { $$ = new T5X_LOCKEXP; $$->SetNot($3);      }
|
    INTEGER                      { $$ = new T5X_LOCKEXP; $$->SetRef($1);      }
|
    STRING ':' STRING            { $$ = new T5X_LOCKEXP; $$->SetAttr($1, $3); }
|
    INTEGER ':' STRING           { $$ = new T5X_LOCKEXP; $$->SetAttr($1, $3); }
|
    STRING '/' STRING            { $$ = new T5X_LOCKEXP; $$->SetEval($1, $3); }
|
    INTEGER '/' STRING           { $$ = new T5X_LOCKEXP; $$->SetEval($1, $3); }
;

attrlist
:
    attrlist attr       { $$ = $1; if (NULL == $$) { $$ = new vector<T5X_ATTRINFO *>; } $$->push_back($2); }
|
                        { $$ = NULL; }
;


attr
:
    '>' INTEGER STRING  { $$ = new T5X_ATTRINFO; $$->SetNumAndValue($2, $3); }
;

%%

void t5xerror(const char *s)
{
    extern int t5xlineno;
    fprintf(stderr, "%s near line %d.\n", s, t5xlineno);
}
