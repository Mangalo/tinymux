#
# secure_fn.mux - Test Cases for secure().
# $Id$
#
# Strategy: Exercise color and UTF-8 in combination with secure() behaviors.
#
@create test_secure_fn
-
@set test_secure_fn=INHERIT QUIET
-
#
# Beginning of Test Cases
#
&tr.tc000 test_secure_fn=
  @log smoke=Begining secure() test cases.
-
#
# Test Case #1 - Test that characters [](){};%\$ mixed with ASCII are replaced with spaces.
#
&tr.tc001 test_secure_fn=
  @va me=a%[a%]a%(a%)a%{a%}a%;a%%a%\a%$a;
  @vb me=%[%]%(%)%{%}%;%%%\%$;
  @if strmatch(
        setr(0,sha1(
            secure(%va)
            [secure(%vb)]
          )
        ),
        11C8262AE1CC277C48765D3C92EB225B21D669E9
      )=
  {
    @log smoke=TC001: ASCII mix. Succeeded.
  },
  {
    @log smoke=TC001: ASCII mix. Failed (%q0).
  }
-
#
# Test Case #2 - Test that characters [](){};%\$ mixed with color and ASCII are replaced with spaces.
#
&tr.tc002 test_secure_fn=
  @va me=%xga%xr%[%xga%xr%]%xga%xr%(%xga%xr%)%xga%xr%{%xga%xr%}%xga%xr%;%xga%xr%%%xga%xr%\%xga%xr%$%xga;
  @vb me=%xg%[%xr%]%xg%(%xr%)%xg%{%xr%}%xg%;%xr%%%xg%\%xr%$%xg;
  @if strmatch(
        setr(0,sha1(
            translate(secure(%va),p)
            [translate(secure(%vb),p)]
          )
        ),
        D81C50C590F8D4C41E5DE642DABA8C98EC8B5154
      )=
  {
    @log smoke=TC002: ASCII-color mix. Succeeded.
  },
  {
    @log smoke=TC002: ASCII-color mix. Failed (%q0).
  }
-
#
# Test Case #3 - Test that characters [](){};%\$ mixed with Unicode are replaced with spaces.
#
&tr.tc003 test_secure_fn=
  @if chr(256)=
  {
    @va me=[chr(256)]%[[chr(256)]%][chr(256)]%([chr(256)]%)[chr(256)]%{[chr(256)]%}[chr(256)]%;[chr(256)]%%[chr(256)]%\[chr(256)]%$[chr(256)];
    @vb me=[chr(10010)]%[[chr(10010)]%][chr(10010)]%([chr(10010)]%)[chr(10010)]%{[chr(10010)]%}[chr(10010)]%;[chr(10010)]%%[chr(10010)]%\[chr(10010)]%$[chr(10010)];
    @if strmatch(
          setr(0,sha1(
              secure(%va)
              [secure(%vb)]
            )
          ),
          3D295519E39B3FDDF0942D5D583B85A1E716CA1C
        )=
    {
      @log smoke=TC003: Unicode mix. Succeeded.
    },
    {
      @log smoke=TC003: Unicode mix. Failed (%q0).
    };
    @trig me/tr.done
  },
  {
    @log smoke=TC003: Unicode is not supported on this version. Okay.;
    @trig me/tr.done
  }
-
&tr.done test_secure_fn=
  @log smoke=End secure() test cases.;
  @notify smoke
-
drop test_secure_fn
-
#
# End of Test Cases
#
